// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin:  'java'
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

//have to make sure the appengineRun task is in daemon mode or it will hang, see
//https://stackoverflow.com/questions/31434928/android-gradle-task-google-appengine
//and
//https://github.com/GoogleCloudPlatform/gradle-appengine-plugin#convention-properties
task startServer(dependsOn:  ":jokeendpoint:appengineRun") {
    project(":jokeendpoint").afterEvaluate { backend ->
        backend.extensions.appengine.daemon = true
    }
    doLast {
        println "Started dev server."
    }
}

task runAllTests(dependsOn: [':app:check', ':app:connectedCheck', ':jokelibrary:check',
                             ':jokedisplaylibrary:connectedCheck', ':jokedisplaylibrary:check']) {
    doLast {
        println "Completed running tests."
    }
}

task runAllTestsWithServer(dependsOn: ['startServer', 'runAllTests']) {
    group = "verification"
    mustRunAfter "startServer"
    tasks.runAllTests.mustRunAfter "startServer"
    finalizedBy ":jokeendpoint:appengineStop"
}

